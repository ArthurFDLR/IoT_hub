plugins {
	id 'java'
	id 'idea'
	id 'eclipse'
	id 'jacoco'
	id 'org.springframework.boot' version '1.5.22.RELEASE'
	id 'io.spring.dependency-management' version "1.0.8.RELEASE"
}

defaultTasks 'clean', 'test', 'jacocoTestReport'

compileJava.options.encoding = 'UTF-8'
group = 'iot_ece448'
version = '1.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8
buildDir = 'build'

repositories {
	jcenter()
}

dependencies {
	implementation(
		'org.apache.httpcomponents:fluent-hc:4.5.9',
		'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.2',
		'org.springframework.boot:spring-boot-starter-web',
		files('lib/sqlite-jdbc-3.32.3.2.jar'))
	testImplementation('junit:junit:4.12')
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
		resources {
			srcDirs = ['src/main/resources']
		}
	}

	test {
		java {
			srcDirs = ['src/test/java']
		}
		resources {
			srcDirs = ['src/test/resources']
		}
	}
}

task clear_mqtt(type:Exec) {
	commandLine 'sh', './clear_mqtt.sh'
}

task iot_sim(type: JavaExec, dependsOn: [test, jacocoTestReport]) {
	classpath sourceSets.main.runtimeClasspath
	main = 'ece448.iot_sim.Main'
	if (project.hasProperty("config"))
		args project.getProperty('config')
}

task iot_sim_ex(type: JavaExec, dependsOn: [test, jacocoTestReport]) {
	classpath sourceSets.main.runtimeClasspath
	main = 'ece448.iot_sim.Main'
	args 'simConfigEx.json'
}

task iot_sim_custom(type: JavaExec, dependsOn: [test, jacocoTestReport]) {
	classpath sourceSets.main.runtimeClasspath
	main = 'ece448.iot_sim.Main'
	args 'simConfigCustom.json'
}

task iot_hub(type: JavaExec, dependsOn: [test, jacocoTestReport]) {
	classpath sourceSets.main.runtimeClasspath
	main = 'ece448.iot_hub.Main'
}

6.times { proj ->
	task "grade_p$proj"(type: JavaExec, dependsOn: [test, jacocoTestReport]) {
		classpath sourceSets.main.runtimeClasspath
		main = "ece448.grading.GradeP$proj"
	}
}

jacoco {
	toolVersion = '0.8.5'
}
